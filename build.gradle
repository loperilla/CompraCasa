// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        // Make sure that you have the following two repositories
        google()  // Google's Maven repository
        mavenCentral()  // Maven Central repository
    }
    dependencies {
        // Add the dependency for the Google services Gradle plugin
        classpath libs.google.services
        classpath libs.crashlytics.gradle
        classpath libs.safe.args.gradle
        classpath libs.kotlin.gradle
    }
}

plugins {
    alias libs.plugins.application apply false
    alias libs.plugins.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.version.catalog.update
    alias libs.plugins.ben.manes.versions
}

// Variable para pillar versiones estables
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// Tarea para setear estas versiones estables al actualizar dependencias
tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}